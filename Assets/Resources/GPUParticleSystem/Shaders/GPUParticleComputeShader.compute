CGPROGRAM

// TODO: Fix comment: Update kernel // James
#pragma kernel UPDATE

StructuredBuffer<float4> gPositionIN;
StructuredBuffer<float4> gVelocityIN;
StructuredBuffer<float4> gLifetimeIN;

RWStructuredBuffer<float4> gPositionOUT;
RWStructuredBuffer<float4> gVelocityOUT;
RWStructuredBuffer<float4> gLifetimeOUT;
RWStructuredBuffer<float4> gColorOUT;
RWStructuredBuffer<float4> gHaloOUT;
RWStructuredBuffer<float4> gScaleOUT;
RWStructuredBuffer<float4> gTransparencyOUT;

uniform int gMaxParticleCount;
uniform float gDeltaTime;

uniform float3 gConstantAcceleration;
uniform float gConstantDrag;

struct Attractor
{

    float3 mPosition;
    float mPower;
    float mMin;
    float mMax;
    float2 padding;

};

uniform int gAttractorCount;
StructuredBuffer<Attractor> gAttractorBuffer;


struct VectorField
{

    float3 mPosition;
    float3 mVector;
    float mMin;
    float mMax;

};
uniform int gVectorFieldCount;
StructuredBuffer<VectorField> gVectorFieldBuffer;

uniform int gSphereColliderCount;
StructuredBuffer<float4> gSphereColliderBuffer;

RWStructuredBuffer<int> gSphereColliderResultBufferWRITE;

// VALUES OVER LIFETIME.
uniform int gColorLifetimeCount;
StructuredBuffer<float4> gColorLifetimeBuffer;

uniform int gHaloLifetimeCount;
StructuredBuffer<float4> gHaloLifetimeBuffer;

uniform int gScaleLifetimeCount;
StructuredBuffer<float4> gScaleLifetimeBuffer;

uniform int gTransparencyLifetimeCount;
StructuredBuffer<float4> gTransparencyLifetimeBuffer;

[numthreads(64,1,1)]
void UPDATE(uint3 id : SV_DispatchThreadID)
{
    int tID = (int)id.x;

    if (tID >= gMaxParticleCount) return;

    float3 pPosition = gPositionIN[tID].xyz;
    float3 pVelocity = gVelocityIN[tID].xyz;
    float2 pLifetime = gLifetimeIN[tID].xy;

    if (pLifetime.x > -0.3f)
    {
        float dt = gDeltaTime;

        // Update position.
        pPosition = pPosition + pVelocity * dt;

        // Update velocity (acceleration).
        pVelocity = pVelocity + gConstantAcceleration * dt;

        // Update velocity (drag).
        pVelocity = pVelocity - pVelocity * (gConstantDrag * dt);

        // Update velocity (attractors).
        int attractorCount = gAttractorCount;
        for (int i = 0; i < attractorCount; ++i)
        {
            Attractor attractor = gAttractorBuffer[i];
            float3 aPosition = attractor.mPosition;
            float aPower = attractor.mPower;
            float aMin = attractor.mMin;
            float aMax = attractor.mMax;

            float3 attractorDirection = aPosition - pPosition;
            float distance = length(attractorDirection);

            if (distance > aMin && distance < aMax)
            {
                attractorDirection = normalize(attractorDirection);
                float distanceFactor = clamp(1.f / (distance * distance), 0, 1.f);
                float attraction = aPower * distanceFactor;
                pVelocity = pVelocity + attractorDirection * (attraction * dt);
            }
        }

        // Update velocity (vector fields).
        int vectorFieldCount = gVectorFieldCount;
        for (int i = 0; i < vectorFieldCount; ++i)
        {
            VectorField vectorField = gVectorFieldBuffer[i];
            float3 vPosition = vectorField.mPosition;
            float3 vVector = vectorField.mVector;
            float vMin = vectorField.mMin;
            float vMax = vectorField.mMax;

            float3 vectorFieldDirection = vPosition - pPosition;
            float distance = length(vectorFieldDirection);

            if(distance > vMin && distance < vMax)
            {
                pVelocity = pVelocity + vVector * dt;
            }

        }

        // Update collider (sphere).
        int sphereColliderCount = gSphereColliderCount;
        if (sphereColliderCount > 0)
        {

            if (tID < sphereColliderCount)
            {
                gSphereColliderResultBufferWRITE[tID] = 0;
            }

            //TODO BARRIER!!

            for (int i = 0; i < sphereColliderCount; ++i)
            {
                float4 sphereCollider = gSphereColliderBuffer[i];
                float3 cPosition = sphereCollider.xyz;
                float cRadius = sphereCollider.w;

                float3 cDirection = cPosition - pPosition;
                float cDistance = length(cDirection);

                if (cDistance < cRadius)
                {
                    // Register collision.
                    InterlockedAdd(gSphereColliderResultBufferWRITE[i], 1);

                    // Bounce vector with collider.
                    float3 oldPosition = pPosition - pVelocity * dt;
                    float3 normal = normalize(oldPosition - cPosition);

                    float3 newVelocity = reflect(pVelocity, normal);
                    float3 newPosition = oldPosition + newVelocity * dt;

                    pPosition = newPosition;
                    pVelocity = newVelocity;
                }
            }
        }

        // UPDATE LIFEIMTE DEPENDENT VARIABLES.
        float currentLifetimeFactor = 1 - pLifetime.x / pLifetime.y; //[0,1]

        float3 pColor;
        float3 pHalo;
        float2 pScale;
        float pTransparency;

        // COLOR OVER LIFETIME.
        for (int i = 1; i < gColorLifetimeCount; ++i)
        {
            if (gColorLifetimeBuffer[i].w > currentLifetimeFactor)
            {
                float4 c0 = gColorLifetimeBuffer[i - 1];
                float4 c1 = gColorLifetimeBuffer[i];

                float lerpFactor = (currentLifetimeFactor - c0.w) / (c1.w - c0.w);

                pColor = c0.xyz * (1 - lerpFactor) + c1.xyz * lerpFactor;

                //exit
                break;
            }
        }

        // HALO OVER LIFETIME.
        for (int i = 1; i < gHaloLifetimeCount; ++i)
        {
            if (gHaloLifetimeBuffer[i].w > currentLifetimeFactor)
            {
                float4 c0 = gHaloLifetimeBuffer[i - 1];
                float4 c1 = gHaloLifetimeBuffer[i];

                float lerpFactor = (currentLifetimeFactor - c0.w) / (c1.w - c0.w);

                pHalo = c0.xyz * (1 - lerpFactor) + c1.xyz * lerpFactor;

                //exit
                break;
            }
        }

        // SCALE OVER LIFETIME.
        for (int i = 1; i < gScaleLifetimeCount; ++i)
        {
            if (gScaleLifetimeBuffer[i].w > currentLifetimeFactor)
            {
                float4 c0 = gScaleLifetimeBuffer[i - 1];
                float4 c1 = gScaleLifetimeBuffer[i];

                float lerpFactor = (currentLifetimeFactor - c0.w) / (c1.w - c0.w);

                pScale = c0.xy * (1 - lerpFactor) + c1.xy * lerpFactor;

                //exit
                break;
            }
        }

        // TRANSPARENCY OVER LIFETIME.
        for (int i = 1; i < gTransparencyLifetimeCount; ++i)
        {
            if (gTransparencyLifetimeBuffer[i].w > currentLifetimeFactor)
            {
                float4 c0 = gTransparencyLifetimeBuffer[i - 1];
                float4 c1 = gTransparencyLifetimeBuffer[i];

                float lerpFactor = (currentLifetimeFactor - c0.w) / (c1.w - c0.w);

                pTransparency = c0.x * (1 - lerpFactor) + c1.x * lerpFactor;

                //exit
                break;
            }
        }

        gColorOUT[tID] = float4(pColor, 0);
        gHaloOUT[tID] = float4(pHalo, 0);
        gScaleOUT[tID] = float4(pScale, 0, 0);
        gTransparencyOUT[tID] = float4(pTransparency, 0, 0, 0);

        // Update lifetime.
        pLifetime.x = pLifetime.x - dt;
    }

    gPositionOUT[tID] = float4(pPosition, 0);
    gVelocityOUT[tID] = float4(pVelocity, 0);
    gLifetimeOUT[tID] = float4(pLifetime.xy, 0, 0);
}

// Emitt kernel
#pragma kernel EMITT

RWStructuredBuffer<float4> gPositionBuffer;
RWStructuredBuffer<float4> gVelocityBuffer;
RWStructuredBuffer<float4> gLifetimeBuffer;

uniform int gEmittIndex;
uniform float3 gPosition;
uniform float3 gVelocity;
uniform float gLifetime;

uniform StructuredBuffer<float3> gEmittMeshVertexBuffer;
uniform StructuredBuffer<int> gEmittMeshIndexBuffer;
uniform int gEmittMeshVertexCount;
uniform int gEmittMeshIndexCount;
uniform int gEmittMeshRandomIndex;
uniform float3 gEmittMeshScale;

[numthreads(1, 1, 1)]
void EMITT(uint3 id : SV_DispatchThreadID)
{
    int index = gEmittIndex;

    float3 emittPosition = float3(0,0,0);
    if (gEmittMeshIndexCount > 0)
        emittPosition = gEmittMeshVertexBuffer[gEmittMeshIndexBuffer[gEmittMeshRandomIndex]];
    else
        emittPosition = gPosition;

    gPositionBuffer[index] = float4(emittPosition, 0);
    gVelocityBuffer[index] = float4(gVelocity, 0);

    float lifetime = gLifetime;
    gLifetimeBuffer[index] = float4(lifetime, lifetime, 0, 0);
}


// Result kernel
#pragma kernel RESULT

StructuredBuffer<int> gGPUColliderResultBufferIN;
RWStructuredBuffer<int> gGPUColliderResultBufferOUT;

StructuredBuffer<int> gSphereColliderResultBufferREAD;

uniform int gGPUColliderCount;
uniform bool gInitZero;

[numthreads(64, 1, 1)]
void RESULT(uint3 id : SV_DispatchThreadID)
{
    int tID = (int)id.x;

    int gpuColliderCount = gGPUColliderCount;

    if (tID >= gpuColliderCount) return;

    int value = gSphereColliderResultBufferREAD[tID];
    
    if (gInitZero)
    {
        gGPUColliderResultBufferOUT[tID] = value;
    }
    else
    {
        gGPUColliderResultBufferOUT[tID] = gGPUColliderResultBufferIN[tID] + value;
    }
}

// Merge particles to one buffer and add sort value to sort buffer.
#pragma kernel MERGE_INITSORT

StructuredBuffer<float4> gLocalSystemPositionIN;
StructuredBuffer<float4> gLocalSystemVelocityIN;
StructuredBuffer<float4> gLocalSystemLifetimeIN;
StructuredBuffer<float4> gLocalSystemColorIN;
StructuredBuffer<float4> gLocalSystemHaloIN;
StructuredBuffer<float4> gLocalSystemScaleIN;
StructuredBuffer<float4> gLocalSystemTransperancyIN;

RWStructuredBuffer<float4> mergePositionOUT;
RWStructuredBuffer<float4> mergeVelocityOUT;
RWStructuredBuffer<float4> mergeLifetimeOUT;
RWStructuredBuffer<float4> mergeColorOUT;
RWStructuredBuffer<float4> mergeHaloOUT;
RWStructuredBuffer<float4> mergeScaleOUT;
RWStructuredBuffer<float4> mergeTransperancyOUT;

// SORT 
struct SortElement
{
    float value;
    int index;
};
StructuredBuffer<SortElement> gSortElementBufferIN;
RWStructuredBuffer<SortElement> gSortElementBufferOUT;

uniform int gLocalParticleCount;
uniform int gOffsetIndex;
uniform float3 gCameraForward;

[numthreads(64, 1, 1)]
void MERGE_INITSORT(uint3 id : SV_DispatchThreadID)
{
    int tID = (int)id.x;

    if (tID >= gLocalParticleCount) return;

    int offsetIndex = gOffsetIndex;

    float4 positionIN = gLocalSystemPositionIN[tID];
    mergePositionOUT[tID + offsetIndex] = positionIN;
    mergeVelocityOUT[tID + offsetIndex] = gLocalSystemVelocityIN[tID];
    mergeLifetimeOUT[tID + offsetIndex] = gLocalSystemLifetimeIN[tID];
    mergeColorOUT[tID + offsetIndex] = gLocalSystemColorIN[tID];
    mergeHaloOUT[tID + offsetIndex] = gLocalSystemHaloIN[tID];
    mergeScaleOUT[tID + offsetIndex] = gLocalSystemScaleIN[tID];
    mergeTransperancyOUT[tID + offsetIndex] = gLocalSystemTransperancyIN[tID];

    SortElement sortElement;
    sortElement.value = dot(positionIN.xyz, -gCameraForward); //Project particle positon on camera forward vector.
    sortElement.index = tID + offsetIndex;
    gSortElementBufferOUT[tID + offsetIndex] = sortElement;
}


// Sort kernel
#pragma kernel SORT

uniform int gJ;
uniform int gK;

[numthreads(64, 1, 1)]
void SORT(uint3 id : SV_DispatchThreadID)
{
    int tID = (int)id.x;

    if (tID >= gMaxParticleCount) return; // Only true if count is less then block size.

    // [Source] https://gist.github.com/mre/1392067#file-bitonic_sort-cu-L50

    int j = gJ;
    int k = gK;

    // Sorting partners: i and ixj
    int i = tID;
    int ixj = (i^j);

    //// The threads with the lowest ids sort the array.
    if (i < ixj)
    {
        SortElement self = gSortElementBufferIN[i];
        SortElement other = gSortElementBufferIN[ixj];

        if ((i & k) == 0)
        {
            // Sort ascending
            if (self.value > other.value)
            {
                // exchange(i,ixj);
                gSortElementBufferOUT[i] = other;
                gSortElementBufferOUT[ixj] = self;
            }
            else
            {
                gSortElementBufferOUT[i] = self;
                gSortElementBufferOUT[ixj] = other;
            }
        }
        else
        {
            // Sort descending
            if (self.value < other.value)
            {
                // exchange(i,ixj);
                gSortElementBufferOUT[i] = other;
                gSortElementBufferOUT[ixj] = self;
            }
            else
            {
                gSortElementBufferOUT[i] = self;
                gSortElementBufferOUT[ixj] = other;
            }
        }
    }
}
